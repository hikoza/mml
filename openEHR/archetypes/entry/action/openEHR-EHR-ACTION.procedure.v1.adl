archetype (adl_version=1.4)
	openEHR-EHR-ACTION.procedure.v1

concept
	[at0000]	-- Procedure
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics, Australia">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2007-03-12">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record information about the activities required to carry out a procedure, including the planning, scheduling, performance, suspension, cancellation, documentation and completion.">
			use = <"Use to record information about the activities required to carry out a procedure, including the planning, scheduling, performance, suspension, cancellation, documentation and completion. This is done by the recording of data against specific activities, as defined by the 'Pathway' careflow steps in this archetype.

The scope of this archetype encompasses activities for a broad range of clinical procedures performed for evaluative, investigative, screening, diagnostic, curative, therapeutic or palliative purposes. Examples range from the relatively simple activities, such as insertion of an intravenous cannula, through to complex surgical operations.

Additional structured and detailed information about the procedure can be captured using purpose-specific archetypes inserted into the 'Procedure detail' slot, where required.

Start date/time is included as a separate data element. If this is recorded against the 'Scheduled' careflow step, it captures the scheduled start time; if recorded against the 'Procedure performed' careflow step, then it captures the actual start time of the procedure.

Finish date/time is also included as a data element, and is intended for use when two separate completion times are required, or when using the archetype to document a performed procedure retrospectively. An example of the former can be when finishing a surgical operation and the operation itself is completed, but activities such as cleaning and dressing wounds, patient positioning, etc, remains before the procedure as a whole is completed.

Within the context of an Operation Report, this archetype will be used to record only what was done during the procedure . Separate archetypes will be used to record the other required components of the Operation Report, including the taking of tissue specimen samples, use of imaging guidance, operation findings, post-operative instructions and plans for followup.

Within the context of a Problem list or summary, this archetype may be used to represent procedures that have been performed. The EVALUATION.problem_diagnosis will be used to represent the patient's problems and diagnoses.

In practice, many procedures (for example, in ambulatory care) will occur once and not be ordered in advance. The pathway step, 'Procedure completed' (or 'Failed attempt', or 'Procedure aborted') will be recorded and the details added. In some cases a recurring procedure will be ordered, and in this situation data against the 'Procedure undertaken' step will be recorded on each occasion, leaving the instruction in the active state. When the last occurrence is recorded the 'Procedure completed' action is recorded showing that this order is now in the completed state.

In other situations, such as secondary care, there may be a formal order for a procedure using a corresponding INSTRUCTION archetype. This ACTION archetype can then be used to record the workflow of when and how the order has been carried out.

Recording information using this ACTION archetype indicates that some sort of activity has actually occurred; this will usually be the procedure itself but may be a failed attempt or another activity such as postponing the procedure. If there is a formal order for the procedure, the state of this order is represented by the Pathway step against which the data is recorded. For example, using this archetype the progressing state of a Gastroscopy order may be recorded through separate entries in the EHR progress notes at each 'Pathway' step:

- record the scheduled Start date/time for the gastroscopy (Procedure scheduled); 
- record the gastroscopy was attempted but failed (Failed attempt); and
- record that the gastroscopy procedure has been completed, including information about the procedure details (Procedure completed).">
			keywords = <"procedure", "intervention", "surgical", "medical", "clinical", "therapeutic", "diagnostic", "cure", "treatment">
			misuse = <"Not to be used to record details about related activities such as use of imaging guidance during the procedure - use specific ACTION archetypes for this purpose.

Not to be used to record a whole operation or procedure report.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <"Anne Pauline Anderssen, Helse Nord RHF, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway", "Silje Ljosland Bakke, Helse Bergen HF, Norway (Editor)", "Lars Bitsch-Larsen, Haukeland University Hospital, Bergen, Norway", "Stephen Chu, NEHTA, Australia (Editor)", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Kari Beate Engseth, Finnmarkssykehuset HF + Klinikk Kirkenes, Norway", "David Evans, Queensland Health, Australia", "Einar Fosse, UNN HF, Norwegian Centre for Integrated Care and Telemedicine, Norway", "Jacquie Garton-Smith, Royal Perth Hospital and DoHWA, Australia", "Andrew Goodchild, NEHTA, Australia", "Megan Hawkins, Mater Health Services, Australia", "Sam Heard, Ocean Informatics, Australia", "Andreas Hering, Helse Bergen HF, Haukeland universitetssjukehus, Norway", "Anca Heyd, DIPS ASA, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Mary Kelaher, NEHTA, Australia", "Sabine Leh, Helse-Bergen, Norway", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Hugh Leslie, Ocean Informatics, Australia", "Hallvard Lærum, Oslo Universitetssykehus HF, Norway", "Mike Martyn, The Hobart Anaesthetic Group, Australia", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "Chris Mitchell, RACGP, Australia", "Stewart Morrison, NEHTA, Australia", "Bjørn Næss, DIPS ASA, Norway", "Michael Osborne, Mater Health Services, Australia", "Chris Pearce, Melbourne East GP Network, Australia", "Rune Pedersen, Universitetssykehuset i Nord Norge, Norway", "Peter Scott, Australia", "Elizabeth Stanick, Hobart Anaesthetic Group, Australia", "John Taylor, NEHTA, Australia", "Richard Townley-O'Neill, NEHTA, Australia", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Editor)">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["references"] = <"Procedure, Draft Archetype [Internet]. National eHealth Transition Authority, Australia, NEHTA Clinical Knowledge Manager [cited: 2015-03-21]. Available from: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.936.">
		["MD5-CAM-1.0.1"] = <"3A25FD3F1925DA20E015F14E5F711D52">
	>

definition
	ACTION[at0000] matches {	-- Prosedyre
		ism_transition matches {
			ISM_TRANSITION[at0034] matches {	-- Prosedyre planlagt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::524]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0034]}		-- Prosedyre planlagt
					}
				}
			}
			ISM_TRANSITION[at0035] matches {	-- *Procedure request sent(en)
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::524]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0035]}		-- *Procedure request sent(en)
					}
				}
			}
			ISM_TRANSITION[at0038] matches {	-- Prosedyre utsatt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::527]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0038]}		-- Prosedyre utsatt
					}
				}
			}
			ISM_TRANSITION[at0039] matches {	-- Prosedyre avlyst
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::528]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0039]}		-- Prosedyre avlyst
					}
				}
			}
			ISM_TRANSITION[at0036] matches {	-- Fastsatt tidspunkt for prosedyre
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::529]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0036]}		-- Fastsatt tidspunkt for prosedyre
					}
				}
			}
			ISM_TRANSITION[at0047] matches {	-- Prosedyre utført
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::245]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0047]}		-- Prosedyre utført
					}
				}
			}
			ISM_TRANSITION[at0040] matches {	-- Prosedyre midlertidig stanset
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::530]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0040]}		-- Prosedyre midlertidig stanset
					}
				}
			}
			ISM_TRANSITION[at0041] matches {	-- Prosedyre avbrutt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::531]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0041]}		-- Prosedyre avbrutt
					}
				}
			}
			ISM_TRANSITION[at0043] matches {	-- Prosedyre fullført
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::532]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0043]}		-- Prosedyre fullført
					}
				}
			}
		}
		description matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0002] matches {	-- Prosedyrenavn
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0049] occurrences matches {0..1} matches {	-- Beskrivelse
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {	-- Prosedyredetaljer
						include
							archetype_id/value matches {/.*/}
						exclude
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.relative_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.inspection(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0059] occurrences matches {0..1} matches {	-- Startdato/tid
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0060] occurrences matches {0..1} matches {	-- Sluttdato/tid
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0061] occurrences matches {0..1} matches {	-- Varighet
						value matches {
							DV_DURATION matches {*}
						}
					}
					ELEMENT[at0058] occurrences matches {0..1} matches {	-- Hastegrad
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0062] occurrences matches {0..*} matches {	-- Multimedia
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0048] occurrences matches {0..1} matches {	-- Resultat
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0006] occurrences matches {0..*} matches {	-- Komplikasjon
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0014] occurrences matches {0..*} matches {	-- Begrunnelse
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Kommentar
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0053] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0054] occurrences matches {0..1} matches {	-- Rekvisisjonsidentifikator
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0055] occurrences matches {0..1} matches {	-- Rekvirent
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0056] occurrences matches {0..1} matches {	-- Mottakers rekvisisjonsidentifikator
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0057] occurrences matches {0..*} matches {	-- Mottaker
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Procedure">
					description = <"A clinical activity carried out for screening, investigative, diagnostic, curative, therapeutic, evaluative or palliative purposes.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Procedure name">
					description = <"Identification of the procedure by name.">
					comment = <"Coding of the specific procedure with a terminology is preferred, where possible.">
				>
				["at0003"] = <
					text = <"Procedure detail">
					description = <"Structured information about the procedure.">
					comment = <"Use to capture detailed, structured information about anatomical location, method & technique, equipment used, devices implanted, results, findings etc.">
				>
				["at0005"] = <
					text = <"Comment">
					description = <"Additional narrative about the activity or care pathway step not captured in other fields.">
				>
				["at0006"] = <
					text = <"Complication">
					description = <"Details about any complication arising from the procedure.">
				>
				["at0014"] = <
					text = <"Reason">
					description = <"Reason that the activity or care pathway step for the identified procedure was carried out.">
					comment = <"For example: the reason for the cancellation or suspension of the procedure.">
				>
				["at0034"] = <
					text = <"Procedure planned">
					description = <"The procedure to be undertaken is planned.">
				>
				["at0035"] = <
					text = <"Procedure request sent">
					description = <"Request for procedure sent.">
				>
				["at0036"] = <
					text = <"Procedure scheduled">
					description = <"The procedure has been scheduled.">
				>
				["at0038"] = <
					text = <"Procedure postponed">
					description = <"The procedure has been postponed.">
				>
				["at0039"] = <
					text = <"Procedure cancelled">
					description = <"The planned procedure has been cancelled prior to commencement.">
				>
				["at0040"] = <
					text = <"Procedure suspended">
					description = <"The procedure has been suspended.">
				>
				["at0041"] = <
					text = <"Procedure aborted">
					description = <"The procedure has been aborted.">
				>
				["at0043"] = <
					text = <"Procedure completed">
					description = <"The procedure has been performed and all associated actions completed.">
				>
				["at0047"] = <
					text = <"Procedure performed">
					description = <"The procedure has been performed.">
				>
				["at0048"] = <
					text = <"Outcome">
					description = <"Outcome of procedure performed.">
					comment = <"Coding with a terminology is preferred, where possible.">
				>
				["at0049"] = <
					text = <"Description">
					description = <"Narrative description about the procedure, as appropriate for the pathway step.">
					comment = <"For example: description about the performance and findings from the the procedure, the failed attempt or the cancellation of the procedure.">
				>
				["at0053"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0054"] = <
					text = <"Requestor order identifier">
					description = <"The local ID assigned to the order by the healthcare provider or organisation requesting the service.">
				>
				["at0055"] = <
					text = <"Requestor">
					description = <"Details about the healthcare provider or organisation requesting the service.">
				>
				["at0056"] = <
					text = <"Receiver order identifier">
					description = <"The ID assigned to the order by the healthcare provider or organisation receiving the request for service. This is also referred to as Filler Order Identifier.">
				>
				["at0057"] = <
					text = <"Receiver">
					description = <"Details about the healthcare provider or organisation receiving the request for service.">
				>
				["at0058"] = <
					text = <"Urgency">
					description = <"Urgency of the procedure.">
					comment = <"Coding with a terminology is preferred, where possible.">
				>
				["at0059"] = <
					text = <"Start date/time">
					description = <"The start date and/or time for the procedure.">
					comment = <"This will indicate the scheduled date/time when recorded against the Scheduled care pathway step or the actual Start date/time in the Procedure performed step.">
				>
				["at0060"] = <
					text = <"Finish date/time">
					description = <"The finish date and/or time for the procedure.">
				>
				["at0061"] = <
					text = <"Duration">
					description = <"Duration of the procedure, (especially for the Procedure performed pathway step).">
				>
				["at0062"] = <
					text = <"Multimedia">
					description = <"Mulitimedia representation of a performed procedure.">
				>
			>
		>
	>
